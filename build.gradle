// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.5.20"
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.2.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.17.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.4.32"
    }
}

plugins {
    id("com.diffplug.gradle.spotless") version "4.5.1"
    id("org.jetbrains.kotlinx.binary-compatibility-validator") version '0.6.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter() // Warning: this repository is going to shut down soon
        maven { url 'https://developer.huawei.com/repo/' }
    }

    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = "S01"
        }
    }
}

subprojects {
    apply plugin: "com.diffplug.gradle.spotless"
    ext.ktlintVer = "0.41.0"

    spotless {
        kotlin {
            target("**/*.kt")
        }

        format 'xml', {
            target '**/*.xml'
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    tasks.whenTaskAdded {
        if (name == "preBuild") {
            mustRunAfter("spotlessCheck")
        }
    }
}

apiValidation {
    ignoredProjects += ["app"]
}
